ext."signing.keyId" = System.getenv("SIGNING_KEY_ID")
ext."signing.password" = System.getenv("SIGNING_KEY_PASSWORD")
ext."signing.secretKeyRingFile" = System.getenv("SIGNING_KEY_PATH")

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'java'
    apply plugin: 'signing'
    repositories {
        mavenCentral()
        maven {
            url 'file://' + System.getenv('MVN_LOCAL_REPO')
        }
    }
}

subprojects {
    if (project.publish == "false") {
        /* skipping projects that should not be published */
        return
    }
    task snapshotVersion {
        doFirst {
            project.version += '-SNAPSHOT'
        }
    }
    task publishSnapshot {
        dependsOn 'snapshotVersion'
        dependsOn 'clean'
        dependsOn 'build'
        dependsOn 'publish'
        tasks.findByName('snapshotVersion').mustRunAfter 'clean'
        tasks.findByName('build').mustRunAfter 'snapshotVersion'
        tasks.findByName('publish').mustRunAfter 'build'
    }
    task publishRelease {
        dependsOn 'clean'
        dependsOn 'build'
        dependsOn 'publish'
        tasks.findByName('build').mustRunAfter 'clean'
        tasks.findByName('publish').mustRunAfter 'build'
    }
    task sourceJar(type: Jar) {
        classifier "sources"
        from sourceSets.main.allJava
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier "javadoc"
        from javadoc.destinationDir
    }
    project.afterEvaluate {
        publishing {
            publications {
                "${project.name}"(MavenPublication) {
                    from project.components.java
                    artifact sourceJar
                    artifact javadocJar
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                    pom {
                        groupId = project.group
                        artifactId = project.artifactId
                        name = project.name
                        description = project.description
                        url = project.url
                        organization {
                            name = organisationName
                            url = organisationUrl
                        }
                        issueManagement {
                            system = issueMgmtSystem
                            url = issueMgmtSystemUrl
                        }
                        licenses {
                            license {
                                name = license
                                url = licenseUrl
                                distribution = licenseDistribution
                            }
                        }
                        scm {
                            url = scmUrl
                            connection = scmConnection
                            developerConnection = scmDevConnection
                        }
                        developers {
                            developer {
                                name = developer
                            }
                        }
                    }
                }
            }
            repositories {
                if (project.version.endsWith("-SNAPSHOT")) {
                    maven {
                        name 'OSS Sonatype Snapshot'
                        url 'https://oss.sonatype.org/content/repositories/snapshots'
                        credentials {
                            username System.getenv('SONATYPE_USERNAME')
                            password System.getenv('SONATYPE_PASSWORD')
                        }
                    }
                } else {
                    maven {
                        name 'OSS Sonatype staging'
                        url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                        credentials {
                            username System.getenv('SONATYPE_USERNAME')
                            password System.getenv('SONATYPE_PASSWORD')
                        }
                    }
                }
                def localRepo = System.getenv('MVN_LOCAL_REPO')
                if (localRepo != null) {
                    maven {
                        url 'file://' + localRepo
                    }
                }
            }
        }
        signing {
            sign publishing.publications."${project.name}"
        }
    }
}
